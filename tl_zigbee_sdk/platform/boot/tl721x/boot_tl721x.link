/********************************************************************************************************
 * @file    boot_tl721x.link
 *
 * @brief   This is the link file for TL721X
 *
 * @author  Driver & Zigbee Group
 * @date    2024
 *
 * @par     Copyright (c) 2024, Telink Semiconductor (Shanghai) Co., Ltd. ("TELINK")
 *
 *          Licensed under the Apache License, Version 2.0 (the "License");
 *          you may not use this file except in compliance with the License.
 *          You may obtain a copy of the License at
 *
 *              http://www.apache.org/licenses/LICENSE-2.0
 *
 *          Unless required by applicable law or agreed to in writing, software
 *          distributed under the License is distributed on an "AS IS" BASIS,
 *          WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *          See the License for the specific language governing permissions and
 *          limitations under the License.
 *
 *******************************************************************************************************/
 
ENTRY(_RESET_ENTRY)
SECTIONS
{
    NDS_SAG_LMA_FLASH = 0x20000000 ;
    . = (0x20000000 + __FW_OFFSET);
    PROVIDE (BIN_BEGIN = .);
    .vectors        : { KEEP(*(.vectors )) }
    .flash_code        : { KEEP(*(.flash_code )) }
    . = 0x00040000;
    .retention_reset    : AT( ALIGN(LOADADDR (.flash_code) + SIZEOF (.flash_code),8)) 
        { KEEP(*(.retention_reset )) }
    PROVIDE (_RETENTION_RESET_VMA_START = ADDR(.retention_reset));
    PROVIDE (_RETENTION_RESET_LMA_START = LOADADDR(.retention_reset));
    PROVIDE (_RETENTION_RESET_VMA_END = .); 

    /*. = ALIGN(8);*/
    /*.retention_data : AT( ALIGN(LOADADDR (.retention_reset) + SIZEOF (.retention_reset),8))*/
    /*	{ KEEP(*(.retention_data )) }*/
    /*PROVIDE (_RETENTION_DATA_VMA_START = ADDR(.retention_data));*/
    /*PROVIDE (_RETENTION_DATA_LMA_START = LOADADDR(.retention_data));*/
    /*PROVIDE (_RETENTION_DATA_VMA_END = .);*/
    
    . = ALIGN(8);
    .ram_code	: AT( ALIGN(LOADADDR (.retention_reset) + SIZEOF (.retention_reset),8)) 
        { KEEP(*(.ram_code )) KEEP(*libgcc.a:save-restore.o) KEEP(*(.exec.itable)) }
    PROVIDE (_RAMCODE_VMA_END = .);
    PROVIDE (_RAMCODE_VMA_START = ADDR(.ram_code));
    PROVIDE (_RAMCODE_LMA_START = LOADADDR(.ram_code));
    PROVIDE (_RAMCODE_SIZE = SIZEOF (.ram_code)); 
    . = ALIGN(LOADADDR (.ram_code) + SIZEOF (.ram_code), 8);
    .text           : AT(ALIGN(LOADADDR (.ram_code) + SIZEOF (.ram_code), 8)) 
        { *(.text .stub .text.* .gnu.linkonce.t.* ) KEEP(*(.text.*personality* )) *(.gnu.warning )  }
    .rodata         : AT(ALIGN(LOADADDR (.text) + SIZEOF (.text), ALIGNOF(.rodata))) 
        { *(.rodata .rodata.* .gnu.linkonce.r.* )}
    .eh_frame_hdr   : AT(ALIGN(LOADADDR (.rodata) + SIZEOF (.rodata), ALIGNOF(.eh_frame_hdr)))
        { *(.eh_frame_hdr ) }
    . = ALIGN(0x20);
    .eh_frame   : AT(ALIGN(LOADADDR (.eh_frame_hdr) + SIZEOF (.eh_frame_hdr), 32))
        { KEEP(*(.eh_frame )) } 
  
    . = (0x00080000*__DRAM_1_EN) + (ALIGN(ADDR(.ram_code) + SIZEOF (.ram_code), 8)*__DRAM_DIS);
    PROVIDE( __global_pointer$ = . + (4K / 2) );
    .data   : AT(ALIGN(LOADADDR (.eh_frame) + SIZEOF (.eh_frame),  ALIGNOF(.data)))
        { *(.data .data.* .gnu.linkonce.d.* ) KEEP(*(.gnu.linkonce.d.*personality* )) SORT(CONSTRUCTORS) 
          *(.srodata.cst16 ) *(.srodata.cst8 ) *(.srodata.cst4 ) *(.srodata.cst2 ) *(.srodata .srodata.* ) *(.sdata .sdata.* .gnu.linkonce.s.* ) *(.sdata2 .sdata2.* .gnu.linkonce.s.* )
        }
    PROVIDE (_DATA_VMA_END = .);    
    PROVIDE (_DATA_VMA_START = ADDR(.data));
    PROVIDE (_DATA_LMA_START = LOADADDR(.data));
    
    . = ALIGN(8);
    PROVIDE (_BSS_VMA_START = .);
    .sbss   (NOLOAD)    : { *(.dynsbss ) *(.sbss .sbss.* .gnu.linkonce.sb.* ) *(.scommon .scommon.* ) }
    .bss    (NOLOAD)    : { *(.dynbss ) *(.bss .bss.* .gnu.linkonce.b.* ) *(COMMON ) . = ALIGN(8); }
    /* When using the S file _FILL_STK function (which clears all the values in the stack space to 0x55), 
     * if _BSS_VMA_END does not have four-byte alignment, there may be an out-of-bounds access beyond _STACK_TOP,
     * resulting in a store access fault.
     */
    PROVIDE (_BSS_VMA_END = ((. + 3) / 4)*4);
    
    .sdk_version    : AT(LOADADDR (.data) + SIZEOF (.data))
        { KEEP(*(.sdk_version )) }

    PROVIDE (BIN_SIZE = LOADADDR(.sdk_version) + SIZEOF(.sdk_version) - BIN_BEGIN);

    . = ALIGN(8);
    /* end is the starting address of the heap, the heap grows upward */
    _end = .;
    PROVIDE (end = .);      
    PROVIDE (_STACK_TOP = 0x0060000 + (0x0020000*__IRAM_2_EN) + (0x0020000*__DRAM_1_EN) + (0x0020000*__DRAM_2_EN));
    PROVIDE (FLASH_SIZE = 0x0100000);
}

ASSERT((BIN_SIZE)<= FLASH_SIZE, "BIN FILE OVERFLOW");
